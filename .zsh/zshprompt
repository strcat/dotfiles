# different prompts for X and TTY
if (( ${+DISPLAY} )); then
	# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
	# [[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh && source ~/download/Sources/powerlevel10k/powerlevel10k.zsh-theme
	[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh && source ~/.config/powerlevel10k/powerlevel10k.zsh-theme
	# source ~/download/Sources/powerlevel10k/powerlevel10k.zsh-theme
	# source ~/p10k-robbyrussell.zsh
else
	# PS{1,2,3}, RPOMPT, ..
	# The "prompt" of the shell.
	#  See zshmisc(1) (/PROMPT EXPANSION) for details.
	#
	# %n         $USERNAME.
	# @          literal '@'
	# %m         machine name.
	# %M         The full machine hostname.
	# %%         %
	# %/         Present working directory ($PWD) (i. e.: /home/$USERNAME)
	# %~         Present working directory ($PWD) (i. e.: ~)
	# %h         Current history event number.
	# %!         Current history event number.
	# %L         The current value of $SHLVL.
	# %S (%s)    Start (stop) standout mode.
	# %U (%u)    Start (stop) underline mode.
	# %B (%b)    Start (stop) boldface mode.
	# %t / %@    Current time of day, in 12-hour, am/pm format.
	# %T         Current time of day, in 24-hour format.
	# %*         Current time of day in 24-hour  format,  with  seconds
	# %N         The  name  of  the  script,  sourced file, or shell
	#            function that zsh is currently executing,
	# %i         The line number currently  being  executed  in  the script
	# %w         The date in day-dd format.
	# %W         The date in mm/dd/yy format.
	# %D         The date in yy-mm-dd format.
	# %D{string} string  is  formatted  using the strftime function (strftime(3))
	# %l         The line (tty) the user is logged in on
	# %?         The  return  code of the last command executed just before the prompt
	# %_         The status of the parser
	# %E         Clears to end of line
	# %#         A  `#'  if  the shell is running with privileges, a `%' if not
	# %v         The  value  of the first element of the psvar array parameter
	# %{...%}    Include a string as a literal escape sequence
	# :          literal ':'
	# %Nc        "relative path", ie last N components of $PWD.
	# >          literal '>'
	#
	# Some examples:
	#  PS1="PS1='%B%n%b@%m:%4c>'"
	#  PS1="%B(%b%n@%m%B)%b : %B(%b%3~%B)%b: "
	#  PS1=$'%{\e[1;31m%}[%n@%m:%~ ]%{\e[0m%} '
	#  PS1=$'%{\e[0;36m%}%n%{\e[0m%}:%{\e[0;31m%}%3~%{\e[0m%}%# ' ## user:~%
	#  PS1=$'%{\e[0;36m%}%n%{\e[0m%}:%{\e[0;31m%}%3~%{\e[0m%}%B>%b ' ## user:~>
	#  PS1='%n@%m:%4c%1v> ';RPS1=$'%{\e[0;36m%}%D{%A %T}%{\e[0m%}' ## user@host:~> ; Day time(hh:mm:ss)
	#  PS1='%B[%b%n%B:%b%~%B]%b$ ' ## [user:~]$
	#  PS1=$'%{\e[0;36m%}%n%{\e[0m%}:%20<..<%~%B>%b ' ## user:..c/vim-common-6.0>
	#  PS1=$'%{\e[0;36m%}%#%{\e[0m%} ';RPS1=$'%{\e[0;31m%}%~%{\e[0m%}' ## % ; ~
	#  PS1=$'%{\e[0;36m%}%n%{\e[0m%}%{\e[0;31m%}%#%{\e[0m%} ';RPS1=$'%{\e[0;31m%}%~%{\e[0m%}' ## user% ; ~
	#  PS1='%# ';RPS1='%B%~%b' ## % ; ~ : no colors
	#  PS1='%n@%m:%B%~%b> ' ## user@host:~> : no colors
	#  PS1=$'%{\e[1;31m%}%B(%b%{\e[0m%}%n@%m%{\e[1;31m%})%{\e[0m%} : %{\e[1;31m%}(%{\e[0m%}%~%{\e[1;31m%})%{\e[0m%}: '
	#  PS1=$'%{\e[0;33m%}[%{\e[0m%}%n%{\e[0;33m%}@%{\e[0m%}%m%{\e[0;33m%}:%{\e[0m%}%~%{\e[0;33m%}]%{\e[0m%}%# '
	#  PS1=$(echo '\033[1m\033[30m(%/)\033[0m\033[39m\n[%n@%m \033[0m\033[34m%~\033[0m\033[39m]%# ')
	#  PS1='%n@%U%m%u %B%30<..<%~%b %(!.#.>)' # user@host (underlined), pwd(bold; max 30 chars.) > or #
	#  PS1=$'%{\e[0;31m%}[%{\e[0;36m%}%n%{\e[0;32m%}@%{\e[0;35m%}%m%{\e[0;34m%}:%{\e[0;33m%}%.%{\e[0;31m%}]%{\e[0;0m%}%# '
	#  PROMPT=$'%n@%m %0(3c,%c,%~) %0(?,%{\e[0;32m%}=%),%{\e[0;31m%}=(%s)%b %# '
	# random colors? sure. no problem ;)
	#  $ setopt prompt_subst ; PROMPT=$'[%{\e[$((color=$((30+$RANDOM % 8))))m%}%n@%m %c%{\e[00m%}]%% '
	#
	# You can use ``promptinit'' for the zsh prompt themes extension. See
	# ``less ${^fpath}/promptinit(N)'' for details. btw. http://aperiodic.net/phil/prompt/
	# is a good prompt introduction for the Z shell
	autoload -U promptinit; promptinit
	if [[ $SSH_CLIENT = *.* || $REMOTEHOST = *.* ]]; then
		RPROMPT=$SSH_CLIENT
		setopt TRANSIENT_RPROMPT
	fi

	autoload -Uz vcs_info
	precmd() {
		psvar=()
		vcs_info
		[[ -n $vcs_info_msg_0_ ]] && psvar[1]="$vcs_info_msg_0_"
	}

	if (( EUID == 0 )); then
		PS1=$'%{\e[0;33m%}%B[%b%{\e[0m%}%n%{\e[0;33m%}%B@%b%{\e[0m%}%m%{\e[0;33m%}:%{\e[0m%}%~%{\e[0;33m%}%B]%b%{\e[0m%}%# '
	else
		case $HOST in
			painless*)
			PS1=$'%{\e[1;31m%}%B(%b%{\e[0m%}%n@%m%{\e[1;31m%})%{\e[0m%} : %{\e[1;31m%}(%{\e[0m%}%~%{\e[1;31m%})%{\e[0m%}: '
			;;
			dreckskind*)
			#PROMPT=$'%{\e[0;31m%}%B[%b%{\e[0m%}%n%{\e[0;31m%}@%{\e[0m%}%m%{\e[0;31m%}%B:%b%{\e[0m%}%~ %0(?,%{\e[0;32m%}:%),%{\e[0;31m%}:(%s)%b%{\e[0;31m%}%B ]%b%{\e[0m%}%# '
			PROMPT=$'%{\e[0;32m%}%B[%b%{\e[0m%}%n%{\e[0;32m%}@%{\e[0m%}%m%{\e[0;32m%}%B:%b%{\e[0m%}%~%{\e[0;32m%}%B]%b%{\e[0m%} %(?..%{\e[0;33m%}%B!%b%{\e[0m%})%# '
			# On 'exit-Status != 0' display a ":(" on the right side.
			#RPROMPT="%(?..:()%"
			RPROMPT="%F{green}%1v%f"
			;;
			fuckingtosh*)
			autoload promptinit; promptinit ; prompt adam2
			;;
			blitzkrieg*)
			PS1=$'%{\e[0;36m%}%n%{\e[0m%}%{\e[0;31m%}%#%{\e[0m%} '
			RPS1=$'%{\e[0;31m%}%~%{\e[0m%}'
			;;
			diehard*)
			PS1=$'%{\e[0;36m%}%n%{\e[0m%}:%{\e[0;31m%}%3~%{\e[0m%}%# '
			;;
			*)
			PROMPT="[%n@%m] "
			RPROMPT="[%~]"
		esac
	fi
fi
